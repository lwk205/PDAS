940 'ALFA CARD
950 CLS
LET I = 1
960 PRINT "YOU CAN ANALYZE THE AIRFOIL AT UP TO 14 ANGLES OF ATTACK."
970 PRINT "AT EACH ALPHA, YOU CAN ANALYZE THE AIRFOIL AT UP TO 5 COMBINATIONS OF"
980 PRINT "REYNOLDS NUMBER AND ROUGHNESS LEVEL."
990 PRINT : INPUT "HOW MANY ANGLES OF ATTACK DO YOU WANT"; ALPHAS
1000 IF ALPHAS > 14 THEN PRINT "TOO MANY.  TRY AGAIN.": GOTO 990
1010 FORMAT$ = "ANGLE OF ATTACK NO. ## ="
1020 FOR I = 1 TO ALPHAS: PRINT USING FORMAT$; I; : INPUT A(I): NEXT I
1030 PRINT #2, "ALFA";
1040 PRINT
1050 INPUT "DO YOU WANT A PRINTOUT OF ALL MOMENT COEFFICIENTS FOR EACH ALPHA"; Q$
1060 IF LEFT$(Q$, 1) = "Y" THEN NUPA = 1 ELSE NUPA = 2
1070 PRINT #2, USING "#"; NUPA;
1080 NUPE = 1
1090 PRINT #2, USING "#"; NUPE;
1100 PRINT
1110 PRINT "DO YOU WANT THE ALPHAS RELATIVE TO"
1120 PRINT "    (1) THE CHORD LINE"
1130 PRINT "    (2) THE ZERO-LIFT LINE"
1140 INPUT "OPTION"; Q
1150 PRINT
1160 PRINT "DO YOU WANT THE PRINTOUT AS"
1170 PRINT "   (1) VELOCITY RATIOS v/V"
1180 PRINT "   (2) PRESSURE COEFFICIENTS Cp"
1190 INPUT "OPTION"; Q1
1200 IF Q = 1 AND Q1 = 1 THEN NUPI = 1
1210 IF Q = 2 AND Q1 = 1 THEN NUPI = 0
1220 IF Q = 1 AND Q1 = 2 THEN NUPI = 3
1230 IF Q = 2 AND Q1 = 2 THEN NUPI = 2
1240 PRINT #2, USING "#"; NUPI;
1250 PRINT #2, USING "###"; ALPHAS;
1260 FOR I = 1 TO ALPHAS: PRINT #2, USING "#####"; INT(A(I) * 100); : NEXT I
1270 PRINT #2, ""
1271 PRINT : INPUT "WANNA GENERATE A PLOT OF THE VELOCITY DISTRIBUTIONS? ", PL$
1272 IF LEFT$(PL$, 1) = "y" OR LEFT$(PL$, 1) = "Y" THEN PRINT #2, "DIAG"
1280 'RE CARD
1290 CLS
1300 PRINT #2, "RE  1";
1310 PRINT "OK.  YOU WILL NOW SELECT UP TO 5 COMBINATIONS OF REYNOLDS NUMBERS"
1320 PRINT "AND ROUGHNESS LEVEL.  THESE CAN BE 5 DIFFERENT RE'S AT VARIOUS"
1330 PRINT "ROUGHNESS LEVELS, OR 1 REYNOLDS NUMBER AT 5 DIFFERENT ROUGHNESS"
1340 PRINT "LEVELS, OR ANY COMBINATION OF THE ABOVE.  (ISN'T THIS FUN!!)"
1350 PRINT : INPUT "HOW MANY WILL YOU BE NEEDING (1-5)"; Q
1360 IF Q > 5 THEN PRINT "TOO MANY.  TRY AGAIN.": GOTO 1350
1370 PRINT : PRINT "YOU CAN PRINTOUT A SUMMARY OF THE BOUNDARY LAYER RESULTS"
1380 PRINT "ALONE, OR A COMPLETE LISTING OF THE BOUNDARY LAYER AS IT DEVELOPS."
1390 PRINT "THIS IS NICE BUT USES A LOT OF PAPER AND SLOWS EVERYTHING"
1400 PRINT "DOWN UNLESS YOU HAVE A VERY LARGE PRINTER BUFFER."
1410 PRINT
1420 PRINT "I'VE DECIDED TO"
1430 PRINT "  (1) PRINT THE SUMMARY ALONE"
1440 PRINT "  (2) PRINT THE WHOLE SHEBANG"
1450 INPUT "OPTION"; Q1
1460 NUPE = 10
1470 IF Q1 = 1 THEN 1560
1480 PRINT
1490 PRINT "OK. GO FOR BROKE."
1500 PRINT "AT EACH STEP IN THE BOUNDARY LAYER ANALYSIS, I WANT"
1510 PRINT "  (1) THE MOMENTUM THICKNESS"
1520 PRINT "  (2) THE REYNOLDS NUMBER OF THE MOMENTUM THICKNESS"
1530 PRINT "  (3) THE DISPLACEMENT THICKNESS"
1540 INPUT "OPTION"; Q1
1550 NUPE = (Q1 + 1) * 10
1560 PRINT #2, USING "##   "; NUPE;
1570 PRINT : PRINT "REYNOLDS NUMBERS ARE ENTERED AS RE TIMES 10^3"
1580 PRINT "FOR EXAMPLE, 5 MILLION WOULD BE ENTERED AS 5000"
1590 PRINT "500,000 WOULD BE ENTERED AS 500"
1600 PRINT "I THINK YOU'VE GOT THE IDEA NOW."
1610 PRINT : PRINT "ROUGNESS LEVELS ARE FROM 0 TO 6."
1620 PRINT "ROUGHNESS OF 0 IS FOR CLEAN WINGS."
1630 PRINT "ROUGHNESS OF 2 IS TYPICAL OF USED, UNWASHED AIRPLANES."
1640 PRINT "ROUGHNESS OF 4 IS TYPICAL OF LOADS OF BUGS OR LIGHT ICE."
1650 PRINT "ROUGHNESS OF 6 IS TYPICAL OF SEVERE DENTS OR HEAVY ICE."
1660 ITRANS = 0: IRE = 0
1670 PRINT : PRINT "YOU CAN SPECIFY THE TRANSITION POINTS ON 2 OF THE 5 CASES."
1680 PRINT "TO USE THIS OPTION, ENTER 99 FOR ROUGHNESS LEVEL."
1690 FOR I = 1 TO Q
1700 PRINT
1710 PRINT USING "REYNOLDS NO. FOR CASE # ="; I; : INPUT RE(I)
1720 INPUT "ENTER ROUGHNESS LEVEL FOR THE ABOVE RE"; ROUGH(I)
1730 IF ROUGH(I) <> 99 THEN 1810
1740 ITRANS = ITRANS + 1
1750 IF ITRANS > 2 THEN PRINT "NOT ALLOWED.": ITRANS = 2: GOTO 1720
1760 PRINT
1770 PRINT "TRANSITION POINTS ARE ENTERED IN DECIMAL, E.G., .05 FOR 5%"
1780 INPUT "ENTER X LOCATION OF TRANSITION, UPPER SURFACE"; XU(ITRANS)
1790 INPUT "ENTER X LOCATION OF TRANSITION, LOWER SURFACE"; XL(ITRANS)
1800 ROUGH(I) = ITRANS - 3
1810 RE = INT(RE(I))
1815 IF ROUGH(I) = 0 THEN MU = 0: GOTO 1820
1817 MU = INT((ROUGH(I) + 3) * 1000)
1820 FORMAT$ = "#####"
1830 PRINT #2, USING FORMAT$; MU; : PRINT #2, USING FORMAT$; RE;
1840 IRE = IRE + 2
1850 NEXT I
1860 IF ITRANS = 0 THEN 1900
1870 WHILE IRE < 10: PRINT #2, "          "; : IRE = IRE + 2: WEND
1880 FOR I = 1 TO ITRANS: XU(I) = INT(XU(I) * 10000): XL(I) = INT(XL(I) * 10000)
1890 PRINT #2, USING FORMAT$; XU(I); : PRINT #2, USING FORMAT$; XL(I); : NEXT I
1900 PRINT #2, ""
1901 PRINT : INPUT "WANNA CD-CL PLOT FOR THOSE REYNOLDS NUMBERS? ", PL$
1902 IF LEFT$(PL$, 1) = "y" OR LEFT$(PL$, 1) = "Y" THEN PRINT #2, "CDCL"
1910 CLS
1920 PRINT "O.K.  THE LAST OPTION LETS YOU PUT A FLAP ON THE AIRFOIL."
1930 PRINT "THIS REQUIRES ANOTHER PANEL METHOD RUN FOR EACH DEFLECTION ANGLE."
1940 PRINT "YOU WILL NEED TO RESPECIFY ALPHAS AND REYNOLDS NUMBERS"
1950 PRINT "FOR EACH DEFLECTION ANGLE.  "
2000 PRINT
2010 INPUT "WANT TO TRY A FLAP CASE"; Q$
2020 IF LEFT$(Q$, 1) <> "Y" THEN 2190
2030 PRINT #2, "FLAP      ";
2040 PRINT "YOU MUST ENTER THE HINGE LOCATION, BOTH X & Y, AS A DECIMAL"
2050 PRINT "FRACTION OF TOTAL AIRFOIL CHORD LENGTH.  E.G., IF THE HINGE"
2060 PRINT "LOCATION IS AT X=70% AND Y= -5.3%, ENTER .70 AND -.053"
2070 INPUT "X LOCATION OF HINGE = "; XLOC
2080 FL = INT((1! - XLOC) * 10000)
2090 PRINT #2, USING FORMAT$; FL;
2100 INPUT "Y LOCATION OF HINGE = "; YLOC
2110 PRINT #2, USING FORMAT$; INT(YLOC * 10000);
2120 PRINT "FOR DEFLECTION ANGLES, NEGATIVE IS AN UPWARD DEFLECTION."
2130 PRINT "E.G., TO DEFLECT DOWN 25 DEG., ENTER 25.  TO DEFLECT UP 25 DEG."
2140 PRINT "ENTER -25"
2150 INPUT "FLAP DEFLECTION = "; DEG
2160 PRINT #2, USING FORMAT$; 400;
2170 PRINT #2, USING FORMAT$; INT(DEG * 100)
2180 CLS : GOTO 940
2190 PRINT #2, "ENDE"
2200 CLOSE 1
2210 CLS
2220 PRINT "ALL DONE."
2230 SYSTEM
2240 END
2250 PRINT "This program prepares a FORTRAN card file for input to Eppler's"
2260 PRINT "airfoil analysis program. It reads the airfoil coordinates"
2262 PRINT "from a file with the airfoil name and arranged in the .DAT format,"
2263 PRINT "and writes its output to a file with the same name"
2265 PRINT "and the extension .PRE.": PRINT
2300 PRINT : RETURN

